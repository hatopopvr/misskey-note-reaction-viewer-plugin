/// @ 0.16.0
### {
  name: "ノートについたリアクションの詳細を閲覧するプラグイン"
  version: "0.1.0"
  author: "はとぽぷ(@hatopop_vr@misskey.io)"
  description: "ノートについたリアクションの詳細を閲覧"
  permissions: ["read:reactions"]
}

// SeverNameの取得
let SERVER_NAME = Mk:url().split('/')[2]

// リアクションから不要な文字を除去１ リアクションを取得
@removeChar(reaction) {
    return reaction.replace(`@.`, ``)
}

// リアクションから不要な文字を除去２ 名称を取得
@removeChar2(reaction) {
    return reaction.replace(`:` ``)
}

// indexに基づいてソート
@sortByIndices(sortedIndices, targetList) {
    let sortedList = []
    each (let i sortedIndices) {
        sortedList.push(targetList[i])
    }
    sortedList
}

// ソート処理
@sortReactionByCount(reactionList, countList) {
    let sortedIndices = []
    for (let i, countList.len) {
        sortedIndices.push(i)
    }

    sortedIndices.sort(@(a, b) {
        return countList[b] - countList[a]
    })

    let sortedReactionList = sortByIndices(sortedIndices, reactionList)
    let sortedCountList = sortByIndices(sortedIndices, countList)

    let sortedObj = {}
    Obj:set(sortedObj, "sortedReactionList", sortedReactionList)
    Obj:set(sortedObj, "sortedCountList", sortedCountList)
    sortedObj
}

// APIからノートのリアクションを取得
@getNoteReactions(note_id, limit, requestMaxCount) {
    let resultList = []
    var result = Mk:api(`notes/reactions` {noteId: note_id, limit: limit})

    each (let res result){
        resultList.push(res)
    }

    var until_id = result[result.len - 1].id
    var count = 1

    loop {
        if count == requestMaxCount break

        var payload = {noteId: note_id, limit: limit, untilId: until_id}

        result = Mk:api(`notes/reactions` payload)
        if (result.len == 0) break

        each (let res result){
            resultList.push(res)
        }

        until_id = result[result.len - 1].id
        count = count + 1
    }

    resultList
}

// メイン処理
@misskeyNoteReationViewer(note) {
    // リアクションの取得
    let reactionsList = getNoteReactions(note.id, 100, 100)

    // リアクションの集計
    let reactionList = []
    let reactionCount = {}

    each (let reactionSet reactionsList) {
        var reaction = removeChar(reactionSet.type)
        var userHost = reactionSet.user.host
        var user = `@{reactionSet.user.username}`

        // リモートユーザー対応
        if (userHost == null) {
            user = `@{reactionSet.user.username}`
        } else {
            user = `@{reactionSet.user.username}@{userHost}`
        }

        // reactionのリストの追加
        if (!reactionList.incl(reaction)) {
            reactionList.push(reaction)
        }

        // reactionCountにreactionがなければ初期化
        if (Obj:has(reactionCount, reaction) == false) {
            Obj:set(reactionCount, reaction, { users: [] })
        }

        let reactionUsers = Obj:get(Obj:get(reactionCount, reaction), 'users')
        reactionUsers.push(user)
    }

    // countListに各リアクション数(ユーザー数)の追加
    let countList = []
    each (let reaction reactionList) {
        let reactionUsers = Obj:get(Obj:get(reactionCount, reaction), 'users')
        countList.push(reactionUsers.len)
    }

    // リアクション数順にソートしたオブジェクト
    let sortedObj = sortReactionByCount(reactionList, countList)
    let sortedReactionList = Obj:get(sortedObj, "sortedReactionList")

    // ダイアログと投稿用テキストの作成
    let dialogTextList = []
    // ダイアログと投稿用テキストの作成
    let dialogTextList2 = []

    each (let reaction sortedReactionList) {
        let reactionName = removeChar2(reaction)
        let reactionUsers = Obj:get(Obj:get(reactionCount, reaction), 'users')
        let dialogUserReactions = reactionUsers.join(" ")

        if (!(reaction).incl("@")) {
            dialogTextList.push(`{reaction}`)
        }
        dialogTextList2.push(`{reaction} {reactionUsers.len} {dialogUserReactions}`)

    }

    // 最終的なダイアログテキスト
    var dialogText1 = dialogTextList.join(" ")
    var dialogText2 = dialogTextList2.join(Str:lf)
    var dialogTextMerged = `{dialogText1}{Str:lf}{Str:lf}**リアションの詳細**{Str:lf}{dialogText2}`

    Mk:dialog(`リアクション抜粋(ローカル)` dialogTextMerged)
}

// リアクションの詳細を取得するボタンの登録
Plugin:register_note_action(`リアクション詳細` @(note) {misskeyNoteReationViewer(note)})